apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'drag-sort-listview'
group = 'asia.ivity.android'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:18.0.+'
}

android {
	compileSdkVersion 19
	buildToolsVersion "19"
	
	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			res.srcDirs = ['res']
		}
	}
}

// TODO: Add a task that prints the test results.
task instrumentTest(dependsOn: connectedCheck)

signing {
    sign configurations.archives
}

if (!hasProperty("sonatypeUsername")) {
    sonatypeUsername = ""
}
if (!hasProperty("sonatypePassword")) {
    sonatypePassword = ""
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            modifyPom(addFilter('aar') { artifact, file ->
                artifact.name == 'drag-sort-listview'
            })
        }
    }
}

def modifyPom(pom) {
    pom.project {
        name 'DragSortListView'
        description 'Android ListView with drag and drop reordering.'
        url 'https://github.com/ened/drag-sort-listview'

        scm {
            url 'https://github.com/ened/drag-sort-listview.git'
            connection 'git@github.com:ened/drag-sort-listview.git'
            developerConnection 'git@github.com:ened/drag-sort-listview.git'
        }

        licenses {
            license {
                name 'BSD License'
                url 'http://opensource.org/licenses/bsd-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bauerca'
                name 'Carl Bauer'
                url 'https://github.com/bauerca'
            }
        }
			}
}
